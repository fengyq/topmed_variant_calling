import os
import math
#import pdb

#pdb.set_trace()

configfile: "config.yml"

rule discover_bcf:
    input:
        config["input_expression"]
    output:
        "out/sm/{sample_id}/{sample_id}.bcf"
        #"out/sm/{sample_id}/{sample_id}.bcf.csi"
    threads: 2
    shell:
        """
        set +e

        tmp_dir=/tmp/snk_`head -c 8 <(pwd | md5sum)`_discover_{wildcards.sample_id}
        mkdir -p $tmp_dir

        set -o pipefail 
        export REF_PATH=resources/ref/md5/%2s/%2s/%s 
        {config[exe_root]}/samtools/samtools view -uh -T resources/ref/hs38DH.fa {input} 2> $tmp_dir/{wildcards.sample_id}.bcf.samtools_err \
          | {config[exe_root]}/bamUtil/bin/bam clipoverlap --poolSize 100000000 --in -.ubam --out -.ubam 2> $tmp_dir/{wildcards.sample_id}.bcf.bamUtil_err \
          | {config[exe_root]}/vt-topmed/vt discover2 -z -q 20 -b + -r resources/ref/hs38DH.fa -s {wildcards.sample_id} -o $tmp_dir/{wildcards.sample_id}.bcf 2> $tmp_dir/{wildcards.sample_id}.bcf.vt_err
        rc=$?
        
        if [[ $rc == 0 ]]; then 
          {config[exe_root]}/bcftools/bcftools index -f $tmp_dir/{wildcards.sample_id}.bcf
          rc=$?
        fi
        
        mv $tmp_dir/*_err out/sm/{wildcards.sample_id}/        
        if [[ $rc == 0 ]]; then 
          mv $tmp_dir/{wildcards.sample_id}.bcf $tmp_dir/{wildcards.sample_id}.bcf.csi out/sm/{wildcards.sample_id}/
          rc=$?
        fi

        rm -r $tmp_dir
        exit $rc
        """
        
rule sample_qc:
    input:
        config["input_expression"]
    output:
        "out/sm/{sample_id}/{sample_id}.vb2"
    threads: 1
    shell:
        """
        set +e

        tmp_dir=/tmp/snk_`head -c 8 <(pwd | md5sum)`_vb2_{wildcards.sample_id}
        mkdir -p $tmp_dir

        export REF_PATH=resources/ref/md5/%2s/%2s/%s
        
        {config[exe_root]}/cramore/cramore cram-verify-bam \
          --svd resources/ref/HGDP_938.b38.genotypes.svd \
          --sam {input} \
          --cap-DP 100 \
          --out $tmp_dir/{wildcards.sample_id}.vb2 \
          --num-PC 4 \
          > $tmp_dir/{wildcards.sample_id}.vb2.stdout \
          2> $tmp_dir/{wildcards.sample_id}.vb2.stderr
        rc=$?

        mv $tmp_dir/*.stderr $tmp_dir/*.stdout out/sm/{wildcards.sample_id}/
        if [[ $rc == 0 ]]; then
          mv $tmp_dir/{wildcards.sample_id}.vb2 out/sm/{wildcards.sample_id}/
          rc=$?
        fi

        rm -r $tmp_dir
        exit $rc
        """

rule discover_bcf_xy_depths:
    input:
        rules.discover_bcf.output #"out/sm/{sample_id}/{sample_id}.bcf"
    output:
        "out/sm/{sample_id}/{sample_id}.norm.xy"
    threads: 1
    shell:
        """
        set +e
        
        tmp_dir=/tmp/snk_`head -c 8 <(pwd | md5sum)`_norm_{wildcards.sample_id}
        mkdir -p $tmp_dir

        export REF_PATH=resources/ref/md5/%2s/%2s/%s

        {config[exe_root]}/cramore/cramore vcf-normalize-depth --xy \
          --vcf {input} \
          --known resources/ref/1000G_omni2.5.b38.sites.PASS.vcf.gz \
          --gc resources/ref/hs38DH.gc.w150.s5.gz \
          --xLabel chrX \
          --yLabel chrY \
          --xStart 2781479 \
          --xStop 15570138 \
          --out $tmp_dir/{wildcards.sample_id}.norm \
          > $tmp_dir/{wildcards.sample_id}.norm.out \
          2> $tmp_dir/{wildcards.sample_id}.norm.err
        rc=$?

        mv $tmp_dir/*.out $tmp_dir/*.err out/sm/{wildcards.sample_id}/
        if [[ $rc == 0 ]]; then
          mv $tmp_dir/{wildcards.sample_id}.norm.xy out/sm/{wildcards.sample_id}/
          rc=$?
        fi

        rm -r $tmp_dir
        exit $rc
        """

def get_batch_sample_ids(wc):
    batch_size = config["batch_size"]
    beg = batch_size * int(wc.batch)
    end = beg + batch_size
    return config["sample_ids"][beg:end]

def get_batch_cram_files(wc):
    batch_size = config["batch_size"]
    beg = batch_size * int(wc.batch)
    end = beg + batch_size
    return [config["input_expression"].format(sample_id=sid) for sid in config["sample_ids"][beg:end]]


def get_batch_norm_files(wc):
    return ["out/sm/" + sid + "/" + sid + ".norm.xy" for sid in get_batch_sample_ids(wc)]

def get_batch_vb_files(wc):
    return ["out/sm/" + sid + "/" + sid + ".vb2" for sid in get_batch_sample_ids(wc)]

rule group_vb_xy_table:
    input:
        get_batch_norm_files, get_batch_vb_files
    params:
        cram_files = get_batch_cram_files,
        sample_ids = get_batch_sample_ids
    output:
        cram_list = "out/index/cram_list_b{batch}.tsv",
        table = "out/index/vb_xy_{batch}.tsv"
    threads: 1
    shell:
        """
        #set +e
        tmp_dir=`mktemp -d`

        echo {params.sample_ids} | xargs -n1 > $tmp_dir/ids.txt
        echo $tmp_dir/ids.txt
        echo {params.cram_files} | xargs -n1 > $tmp_dir/paths.txt
        echo $tmp_dir/paths.txt
        paste $tmp_dir/ids.txt $tmp_dir/paths.txt > {output.cram_list}
        {config[exe_root]}/apigenome/bin/cram-vb-xy-index --index {output.cram_list} --dir out/sm --out {output.table}
        rc=$?
        rm -r $tmp_dir
        exit $rc
        """

rule batch_site_list:
    input:
       rules.group_vb_xy_table.output.table #"out/index/vb_xy_{batch}.tsv"
    output:
        "out/union/b{batch}/b{batch}_{chrom}_{region}.merged.sites.bcf"
    threads: 1
    shell:
        """
        set +e

        tmp_dir=/tmp/snk_`head -c 8 <(pwd | md5sum)`_$(basename {output} .bcf)
        mkdir -p $tmp_dir

        {config[exe_root]}/cramore/cramore vcf-merge-candidate-variants \
          --in-vcf-list <(cut -f 3 {input} | tail -n+2) \
          --region "{wildcards.chrom}:{wildcards.region}" \
          --out-vcf $tmp_dir/out.bcf \
          > $tmp_dir/out.bcf.out \
          2> $tmp_dir/out.bcf.err \
        && {config[exe_root]}/bcftools/bcftools index $tmp_dir/out.bcf
        
        rc=$?
        
        mv $tmp_dir/out.bcf.out {output}.out
        mv $tmp_dir/out.bcf.err {output}.err
        if [[ $rc == 0 ]]; then
          mv $tmp_dir/out.bcf {output} && mv $tmp_dir/out.bcf.csi {output}.csi
          rc=$?
        fi


        rm -r $tmp_dir
        exit $rc
        """

def get_batch_site_files(wc):
    num_batches = math.ceil(len(config["sample_ids"]) / config["batch_size"])
    ret = []
    for b in range(0, num_batches):
        ret.append(("out/union/b{}/b{}_" + wc.chrom + "_" + wc.region + ".merged.sites.bcf").format(b,b))
    return ret
    

rule union_site_list:
    input:
        get_batch_site_files
    output:
        "out/union/union.{chrom}_{region}.merged.sites.bcf",
        "out/union/union.{chrom}_{region}.sites.bcf"
    threads: 2
    shell:
        """
        {config[exe_root]}/cramore/cramore vcf-merge-candidate-variants \
          --in-vcf-list <(echo {input} | tr ' ' '\n') \
          --region {wildcards.chrom}:{wildcards.region} \
          --out-vcf {output[0]} \
          > {output[0]}.out 2> {output[0]}.err

        {config[exe_root]}/bcftools/bcftools index -f {output[0]}
        {config[exe_root]}/vt-topmed/vt annotate_indels -r resources/ref/hs38DH.fa {output[0]} -o + 2> {output[0]}.annotate.err \
          | {config[exe_root]}/vt-topmed/vt consolidate_variants + -o {output[1]} > {output[1]}.out 2> {output[1]}.err
        {config[exe_root]}/bcftools/bcftools index -f {output[1]}
        """

def get_regions_for_contig(c_length, region_size):
    ret = []
    num_regions = int(math.ceil(c_length / region_size))
    for i in range(0, num_regions):
        start = i * region_size
        end = start + region_size
        ret.append(str(start + 1) + "-" + str(min(end, c_length)))
    return ret

def get_region_strings(contigs):
    ret = []
    region_size = config["region_size"]
    for c, c_length in contigs.items():
        for r in get_regions_for_contig(c_length, region_size):
            ret.append(c + "_" + r)
    return ret

rule all_union_site_lists:
    input:
        [ancient("out/union/union." + r + ".sites.bcf") for r in get_region_strings(config["contigs"])]

rule genotyped_batch:
    input:
        cram_list = rules.group_vb_xy_table.output.cram_list, #"out/index/cram_list_b{batch}.tsv",
        sex_map = rules.group_vb_xy_table.output.table, #"out/index/vb_xy_{batch}.tsv",
        sites_bcf = rules.union_site_list.output[1] #"out/union/union.{chrom}_{region}.sites.bcf"
    output:
        "out/genotypes/b{batch}/b{batch}.{chrom}_{region}.genotypes.bcf"
    threads: 1
    shell:
        """
        # out : log/batch-geno
        # list : BATCH : index/seq.batches.by.20.txt
        # list : INTERVAL : index/intervals/b38.intervals.X.10Mb.10Mb.txt
        # var : ROOT : ..
        # var : PREFIX : out/genotypes/batches/$BATCH$1$/b$BATCH$1$.$INTERVAL$1$_$INTERVAL$2$_$INTERVAL$3$
        # name : example-batch-genotype
        # target : $PREFIX$.genotypes.bcf $PREFIX$.genotypes.bcf.csi
        #mkdir -p out/genotypes/batches/$BATCH$1$/
        #cut -f 1,20 out/index/list.107.local.crams.vb_xy.index | tail -n +2 | tail -n +$BATCH$1$ | head -n 20 > $PREFIX$.sex_map.txt
        #cut -f 1,2,5 out/index/list.107.local.crams.vb_xy.index | tail -n +2 | tail -n +$BATCH$1$ | head -n 20 > $PREFIX$.cram_index.txt
        tmp_dir=`mktemp -d`
        set +e

        cut -f 1,20 {input.sex_map} | grep -v "^SAMPLE_ID" > ${{tmp_dir}}/sex_map.txt
        cut -f 1,2,5 {input.sex_map} | grep -v "^SAMPLE_ID" > ${{tmp_dir}}/cram_index.txt
       
        tmp_out=${{tmp_dir}}/$(basename {output})
 
        export REF_PATH=resources/ref/md5/%2s/%2s/%s 
        {config[exe_root]}/cramore/cramore dense-genotype \
          --in-cram-list ${{tmp_dir}}/cram_index.txt \
          --in-vcf {input.sites_bcf} \
          --unit 6000000 \
          --region {wildcards.chrom}:{wildcards.region} \
          --sex-map ${{tmp_dir}}/sex_map.txt \
          --xLabel chrX \
          --yLabel chrY \
          --xStart 2781479 \
          --xStop 155701383 \
          --print-tmp-info \
          --out ${{tmp_out}} \
          --min-mq 1 \
          > ${{tmp_out}}.out 2> ${{tmp_out}}.err &&
        {config[exe_root]}/bcftools/bcftools index -f ${{tmp_out}}
        rc=$?
        mv ${{tmp_out}}.out ${{tmp_out}}.err $(dirname {output})/
        if [[ $rc == 0 ]]; then
          mv ${{tmp_out}} ${{tmp_out}}.csi $(dirname {output})/
          rc=$?
        fi
        rm -r $tmp_dir
        exit $rc
        """

rule entire_batch:
    input:
       ["out/genotypes/b{batch}/b{batch}." + r + ".genotypes.bcf" for r in get_region_strings(config["contigs"])]
    output:
        "out/b{batch}.done"
    threads: 1
    shell:
        """
        touch {output}
        """

rule all_batches:
    input:
        ["out/b" + str(b) + ".done" for b in range(0, math.ceil(len(config["sample_ids"]) / config["batch_size"]))]

rule pasted_genotype_region:
    input:
        ["out/genotypes/b"+ str(b) + "/b" + str(b) + ".{chrom}_{region}.genotypes.bcf" for b in range(0, math.ceil(len(config["sample_ids"]) / config["batch_size"]))]
    output:
        "out/genotypes/merged/{chrom}/merged.{chrom}_{region}.genotypes.bcf"
    threads: 1
    shell:
        """
        # out : log/paste-geno
        # list : INTERVAL : index/intervals/b38.intervals.X.10Mb.1Mb.txt
        # var : ROOT : ..
        # var : PREFIX : $INTERVAL$1$/merged.$INTERVAL$1$_$INTERVAL$4$_$INTERVAL$5$
        # name : example-paste-genotype
        # target : out/genotypes/merged/$PREFIX$.genotypes.bcf out/genotypes/merged/$PREFIX$.genotypes.bcf.csi out/genotypes/minDP0/$PREFIX$.gtonly.minDP0.bcf out/genotypes/minDP0/$PREFIX$.gtonly.minDP0.bcf.csi out/genotypes/minDP10/$PREFIX$.gtonly.minDP10.bcf out/genotypes/minDP10/$PREFIX$.gtonly.minDP10.bcf.csi out/genotypes/hgdp/$PREFIX$.gtonly.minDP0.hgdp.bcf out/genotypes/hgdp/$PREFIX$.gtonly.minDP0.hgdp.bcf.csi
        set +e
        tmp_dir=`mktemp -d`

        #mkdir -p out/genotypes/merged/{wildcards.chrom}/
        #mkdir -p out/genotypes/minDP0/{wildcards.chrom}/
        #mkdir -p out/genotypes/minDP10/{wildcards.chrom}/
        #mkdir -p out/genotypes/hgdp/{wildcards.chrom}/
        
        cut -f1,20 `ls -v out/index/vb_xy_*.tsv` | grep -v "^SAMPLE_ID" > ${{tmp_dir}}/sex_map.txt
        echo {input} | tr ' ' '\n' > ${{tmp_dir}}/merged.bcflist.txt
 
        merged_file=${{tmp_dir}}/$(basename {output})

        {config[exe_root]}/cramore/cramore vcf-paste-calls \
          --vcf-list ${{tmp_dir}}/merged.bcflist.txt \
          --num-pc 0 \
          --sex-map ${{tmp_dir}}/sex_map.txt \
          --xLabel chrX --yLabel chrY \
          --mtLabel chrM \
          --xStart 2781479 --xStop 155701383 \
          --skip-tmp-info \
          --region {wildcards.chrom}:{wildcards.region} \
          --out ${{merged_file}} \
          > ${{merged_file}}.out 2> ${{merged_file}}.err &&
        {config[exe_root]}/bcftools/bcftools index -f ${{merged_file}}
        rc=$?
        mv ${{merged_file}}.out ${{merged_file}}.err $(dirname {output})/
        
        if [[ $rc == 0 ]]; then
          mv ${{merged_file}} ${{merged_file}}.csi $(dirname {output})/
          rc=$?
        fi
        rm -r ${{tmp_dir}}
        exit $rc
        """

rule mindp_region:
    input:
        rules.pasted_genotype_region.output
    output:
        "out/genotypes/minDP{min_dp}/{chrom}/merged.{chrom}_{region}.gtonly.minDP{min_dp}.bcf"
    threads: 1
    shell: 
        """
        set +e
        tmp_dir=`mktemp -d`

        mindp_file=${{tmp_dir}}/$(basename {output})
        {config[exe_root]}/cramore/cramore vcf-squeeze \
          --in {input} \
          --minDP {wildcards.min_dp} \
          --out ${{mindp_file}} \
          > ${{mindp_file}}.out 2> ${{mindp_file}}.err &&
        {config[exe_root]}/bcftools/bcftools index -f ${{mindp_file}}
        rc=$?
        mv ${{mindp_file}}.out ${{mindp_file}}.err $(dirname {output})/

        if [[ $rc == 0 ]]; then
          mv ${{mindp_file}} ${{mindp_file}}.csi $(dirname {output})/
          rc=$?
        fi
        rm -r ${{tmp_dir}}
        exit $rc
        """

rule hgdp_region:
    input:
        "out/genotypes/minDP0/{chrom}/merged.{chrom}_{region}.gtonly.minDP0.bcf"
    output:
        "out/genotypes/hgdp/{chrom}/merged.{chrom}_{region}.gtonly.minDP0.hgdp.bcf"
    threads: 1
    shell:
        """
        set +e
        tmp_dir=`mktemp -d`
        hgdp_file=${{tmp_dir}}/$(basename {output})

        {config[exe_root]}/cramore/cramore vcf-extract \
          --vcf {input} \
          --site resources/ref/HGDP_938.hg38.sites.vcf.gz \
          --out ${{hgdp_file}} \
          > ${{hgdp_file}}.out 2> ${{hgdp_file}}.err &&
        {config[exe_root]}/bcftools/bcftools index -f ${{hgdp_file}}
        rc=$?
        mv ${{hgdp_file}}.out ${{hgdp_file}}.err $(dirname {output})/

        if [[ $rc == 0 ]]; then
          mv ${{hgdp_file}} ${{hgdp_file}}.csi $(dirname {output})/
          rc=$?
        fi
        rm -r ${{tmp_dir}}
        exit $rc
        """

rule all_pasted_genotypes:
    input:
        ["out/genotypes/hgdp/" + r.split("_")[0] + "/merged." + r + ".gtonly.minDP0.hgdp.bcf" for r in get_region_strings(config["contigs"])] + ["out/genotypes/minDP10/" + r.split("_")[0] + "/merged." + r + ".gtonly.minDP10.bcf" for r in get_region_strings(config["contigs"])]

def get_autosome_region_strings(contigs, autosome_contigs):
    ret = []
    region_size = config["region_size"]
    for c in autosome_contigs:
        for r in get_regions_for_contig(contigs[c], region_size):
            ret.append(c + "_" + r)
    return ret

rule concatenated_hgdp_autosomes:
    input:
        ["out/genotypes/hgdp/" + r.split("_")[0] + "/merged." + r + ".gtonly.minDP0.hgdp.bcf" for r in get_autosome_region_strings(config["contigs"], config["autosome_contigs"])]
    output:
        "out/genotypes/hgdp/merged.autosomes.gtonly.minDP0.hgdp.bcf"
    threads: 1
    shell:
        """
        {config[exe_root]}/bcftools/bcftools concat -n -Ob -o {output} {input}
        """

rule autosomes_bed_file:
    input: rules.concatenated_hgdp_autosomes.output
    output: "out/genotypes/hgdp/merged.autosomes.gtonly.minDP0.hgdp.plink.bed"
    shell: 
        """
        output_file={output}
        plink-1.9 --bcf {input} --make-bed --out ${{output_file%.bed}} --allow-extra-chr
        """

rule kinship:
    input: rules.autosomes_bed_file.output
    output: "out/genotypes/hgdp/merged.autosomes.gtonly.minDP0.hgdp.king.kin0"
    threads: 1
    shell:
        """
        output_file={output}
        {config[exe_root]}/king/king -b {input} --degree 4 --kinship --prefix ${{output_file%.kin0}}
        """

rule pedigree:
    input: rules.kinship.output
    output: "out/genotypes/hgdp/merged.autosomes.gtonly.minDP0.hgdp.king.inferred.ped"
    threads: 1
    shell:
        """
        cut -f1,20 `ls -v out/index/vb_xy_*.tsv` | grep -v "^SAMPLE_ID" > out/index/sex_map.txt
        {config[exe_root]}/apigenome/bin/vcf-infer-ped --kin0 {input} --sex out/index/sex_map.txt --out {output}
        """

rule milk_filtered_region:
    input: 
        pedigree = rules.pedigree.output,
        bcf = "out/genotypes/merged/{chrom}/merged.{chrom}_{region}.genotypes.bcf"
    output:
        "out/milk/{chrom}/milk.{chrom}_{region}.full.vcf.gz"
    threads: 1
    shell:
        """
        set +e
        tmp_dir=`mktemp -d`
        tmp_out=${{tmp_dir}}/$(basename {output})

        {config[exe_root]}/vt-topmed/vt milk_filter \
          -f {input.pedigree} \
          -b {input.bcf} \
          -o ${{tmp_out}} \
          -g out/index/sex_map.txt \
          --xLabel chrX --yLabel chrY --mtLabel chrM \
          --xStart 2781479 --xStop 155701383 \
          --af-field AF &&
        zcat ${{tmp_out}} | cut -f 1-8 | {config[exe_root]}/htslib/bgzip -c > ${{tmp_dir}}/$(basename {output} .full.vcf.gz).sites.vcf.gz
        rc=$?
        
        if [[ $rc == 0 ]]; then
          mv ${{tmp_dir}}/* $(dirname {output})/
          rc=$?
        fi
        
        rm -r ${{tmp_dir}}
        exit $rc
        """

def get_concatenated_milk_chromome_input(wc):
    return ["out/milk/" + wc.chrom + "/milk." + wc.chrom + "_" + r + ".full.vcf.gz" for r in get_regions_for_contig(config["contigs"][wc.chrom], config["milk_region_size"])]

rule concatenated_milk_chromosome:
    input:
       get_concatenated_milk_chromome_input
    output:
       "out/milk/milk.{chrom}.sites.vcf.gz"
    threads: 1
    shell:
        """
        set +e
        tmp_dir=`mktemp -d`
        tmp_out=$tmp_dir/$(basename {output})

        {config[exe_root]}/bcftools/bcftools concat {input} -Oz -o $tmp_out &&
        {config[exe_root]}/htslib/tabix -f -pvcf $tmp_out
        rc=$?
        
        if [[ $rc == 0 ]]; then
          mv $tmp_dir/* $(dirname {output})/
          rc=$?
        fi

        rm -r $tmp_dir
        exit $rc
        """

rule all_concatenated_milk_chromosomes:
    input:
        ["out/milk/milk." + c + ".sites.vcf.gz" for c, l in config["contigs"].items()]

















foo = "bar"

